#
# Classy::Toplevel
#

proc ::Classy::WindowBuilder::start_Classy::Toplevel {object base} {
	private $object data bindtags
	set bindtags($base) [bindtags $base]
	$object startedit [winfo children $base]
	foreach child [winfo children $base] {
		if [regexp "^$base.#classy__#menu_" $child] {
			set data(redir,$child) $base
		}
	}
	bindtags $base $data(tags)
	if ![info exists data(opt-destroycommand,$base)] {
		set data(opt-destroycommand,$base) ""
	}
	$base configure -destroycommand ""
	$object select $base
	wm protocol $base WM_DELETE_WINDOW [varsubst base {
		catch {$base configure -destroycommand {}}
		catch {destroy $base}
	}]
}

proc ::Classy::WindowBuilder::attr_Classy::Toplevel_-destroycommand {object w args} {
	private $object data
	if {"$args" == ""} {
		return $data(opt-destroycommand,$w)
	} else {
		set value [lindex $args 0]
		if ![info exists data(opt-destroycommand,$w)] {
			if {"$value" != ""} {
				set data(opt-destroycommand,$w) "\"$value\""
			}
		}
	}
}

proc ::Classy::WindowBuilder::edit_Classy::Toplevel {object w} {
	set c [$object current]
	eval destroy [winfo children $w]
	frame $w.general
	::Classy::WindowBuilder::defattredit $object $w.general {
		-title Title 0 -menu Menu 0 -keepgeometry Keepgeometry 0 -resize Resize 0 -destroycommand destroycommand 1
	} 12 0
	set ::Classy::WindowBuilder::error {}
	grid $w.general -sticky nswe
	grid columnconfigure $w 0 -weight 1
	grid rowconfigure $w 0 -weight 1
}

proc ::Classy::WindowBuilder::generate_Classy::Toplevel {object base} {
	private $object current data
	set body ""
	set outw [$object outw $base]
	append body "\tClassy::Toplevel $outw [$object getoptions $base -menu]\n"
	append body [$object generatebindings $base $outw]
	set children ""
	foreach child [winfo children $base] {
		if [regexp "^$base.classy__\[a-z0-9\]+\$" $child] continue
		if [regexp "^$base.#classy__#menu_" $child] continue
		lappend children $child
	}
	append body [$object generate $children]
	if [info exists data(opt-mainmenu,$base)] {
		if {"$data(opt-mainmenu,$base)" != ""} {
			if {"$data(opt-menuwin,$base)" != ""} {
				foreach win $data(opt-menuwin,$base) {
					append data(parse) "\tClassy::DynaMenu attachmainmenu $data(opt-mainmenu,$base) $win\n"
				}
			} else {
				append data(parse) "\tClassy::DynaMenu attachmainmenu $data(opt-mainmenu,$base) $outw\n"
			}
		}
	}
	append body [$object gridconf $base]
	append body "\n"
	return $body
}
